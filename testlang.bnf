
<program> ::= <config_opt> <var_list_opt> <test_list>

<config_opt> ::= <config_block> | ε

<config_block> ::= "config" "{" <config_body> "}"

<config_body> ::= <config_item> | <config_body> <config_item>

<config_item> ::= "base_url" "=" <string> ";"
                | "header" <string> "=" <string> ";"

<test_list> ::= <test> | <test_list> <test>

<test> ::= "TEST" <identifier> "{" <test_body> "}"

<test_body> ::= <request_section> <assert_section>

<request_section> ::= "REQUEST" "{" <method_decl> <url_decl> <headers_opt> <body_opt> "}"

<method_decl> ::= "METHOD" ":" <http_method>
<url_decl> ::= "URL" ":" <string>

<headers_opt> ::= "HEADERS" "{" <header_list> "}" | ε
<header_list> ::= <header> | <header_list> <header>
<header> ::= <string> ":" <string>

<body_opt> ::= "BODY" ":" <json> | ε

<assert_section> ::= "ASSERT" "{" <assert_list> "}"
<assert_list> ::= <assert> | <assert_list> <assert>

<assert> ::= "status" "==" <number>
           | "body" "." <json_path> "==" <string>
           | "body" "." <json_path> "contains" <string>

<http_method> ::= "GET" | "POST" | "PUT" | "DELETE"

<var_list_opt> ::= <var_list> | ε

<var_list> ::= <var_decl> | <var_list> <var_decl>

<var_decl> ::= "let" <identifier> "=" <value> ";"

<value> ::= <string> | <number>
